/*
 * Eclipse Notes: Currently the basin service cannot be deployed through
 * Eclipse and must be deployed manually. Eclipse Buildship does not
 * reference Gradle tasks for deployment and relies instead on the project 
 * 'Deployment Assembly', which does not correclty compile dependent
 * projects. 
 * 
 * As a workaround, use the additional 'deploy' task to build a WAR and
 * copy it to the webapps or other deployment directory specified in your 
 * ~/.gradle/gradle.properties file:
 *
 * webapps_loc=path/to/servlet/webapps
 */

apply plugin: 'eclipse-wtp'
apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }

    resources {
      srcDirs = ['src']
      exclude '**/*.java'
    }
  }

  test {
    java {
      srcDirs = ['test']
    }
  }
}

repositories {
  jcenter()
}

dependencies {
  providedCompile 'org.apache.tomcat:tomcat-catalina:8.0.45'
  compile project(':nshmp-haz')
  testCompile 'junit:junit:4.12'
}

war {
  webAppDirName = 'webapp'
}

task deploy(type: Copy, dependsOn: assemble) {
  def webappDir = findProperty("webapps_loc")

  if (webappDir) {
    from file("${buildDir}/libs/nshmp-site-ws.war")
    into file(webappDir)
  }
}

configure(deploy) {   
  group = 'Build'
  description = 'Publish WAR to webapps'
}
