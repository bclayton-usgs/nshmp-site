/*
 * Eclipse Notes: Currently the basin service cannot be deployed through
 * Eclipse and must be deployed to Tomcat manually. 
 * 
 * The war file can be copied to Tomcat using the 'tomcat' Gradle task. 
 * For the Gradle task to work one must add the following to the Gradle 
 * properties file at ~/.gradle/gradle.properties:
 *
 * tomcat_loc=path/to/tomcat/webapps
 */

apply plugin: 'eclipse-wtp'
apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }

    resources {
      srcDirs = ['src']
      exclude '**/*.java'
    }
  }

  test {
    java {
      srcDirs = ['test']
    }
  }
}

repositories {
  jcenter()
}

dependencies {
  compile project(':nshmp-haz-ws')
}

war {
  webAppDirName = 'webapp'
}

task tomcat(type: Copy, dependsOn: assemble) {
  def tomcatLoc = findProperty("tomcat_loc")

  if (tomcatLoc) {
    from file("${buildDir}/libs/nshmp-site-ws.war")
    into file(tomcatLoc)
  }
}
